// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │           These are originally from LegenDoom. Thanks, Yholl!            │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_GunSmoke : Actor {
   default {
      RenderStyle "Add";
      Alpha 0.5;
      Speed 20;
      Radius 3;
      Height 3;
      Scale 0.3;
      Projectile;
      +NoInteraction
      +ThruActors
      +ForceXyBillboard
   }
   override void postBeginPlay() {
      super.postBeginPlay();
      alpha = fRandom[Lith_GunSmoke](.1, .4);
      vel *= .01;
      scale *= fRandom[Lith_GunSmoke](.07, .14);
      if(random[Lith_GunSmoke](0, 255) < 128) scale.x = -scale.x;
   }
   override void tick() {
      setOrigin(pos + vel, true);
      a_fadeOut(0.01/5);
      Lith_UTIL.stateTick(self);
   }
   states {
   Spawn:
      #ESM ABCDEFGHIJKLMNOPQ 5;
      stop;
   }
}

class Lith_GunSmokeBig : Lith_GunSmoke {
   default {
      Scale 0.6;
   }
}

class Lith_GunSmokeEmitter : Actor {
   default {
      Radius 4;
      Height 4;
      Speed 7;
      Damage 0;
      Projectile;
      +ThruActors
      +NoClip
   }
   const flags = SXF_NOCHECKPOSITION|SXF_ORIGINATOR|SXF_ABSOLUTEVELOCITY;
   override void postBeginPlay() {
      for(int i = 0, n = random[Lith_GunSmoke](5, 7); i < n; i++) {
         A_SpawnItemEx("Lith_GunSmoke", fRandom[Lith_GunSmoke](-1,1),fRandom[Lith_GunSmoke](-1,1),fRandom[Lith_GunSmoke](-1,1), zvel: vel.z, flags: flags);
      }
      destroy();
   }
   states {
   Spawn:
      TNT1 A 1;
      stop;
   }
}

class Lith_GunSmokeAndSparks : Lith_GunSmokeEmitter {
   default {
      Lith_GunSmokeAndSparks.Particles 10;
      Lith_GunSmokeAndSparks.SmokeClass "Lith_GunSmoke";
      Lith_GunSmokeAndSparks.Spread 1;
   }
   meta int m_part;
   meta class<Actor> m_class;
   meta double m_spread;
   property Particles: m_part;
   property SmokeClass: m_class;
   property Spread: m_spread;
   void lith_spawnSmoke(double vx, double vy) {
      A_SpawnItemEx(m_class, fRandom[Lith_GunSmoke](-4,4)*m_spread,fRandom[Lith_GunSmoke](-1,1)*m_spread,fRandom[Lith_GunSmoke](-2,2)*m_spread, vel.x + vx,vel.y + vy,vel.z, 0, flags);
   }
   void lith_spawnParticles(color c) {
      A_SpawnParticle(c, SPF_FULLBRIGHT|SPF_RELATIVE, random[Lith_GunSmoke](3, 8), random[Lith_GunSmoke](1, 2), fRandom[Lith_GunSmoke](-15, 15), fRandom[Lith_GunSmoke](-1,1)*m_spread,fRandom[Lith_GunSmoke](-1,1)*m_spread,fRandom[Lith_GunSmoke](-1,1)*m_spread, fRandom[Lith_GunSmoke](1, 16),fRandom[Lith_GunSmoke](-0.1, 0.1),(vel.z*1.5)+fRandom[Lith_GunSmoke](-1,1), 0.0, 0.0, -0.05, 0.98, -1);
   }
   override void postBeginPlay() {
      setXyz(pos + vel);
      lith_spawnSmoke(0, fRandom[Lith_GunSmoke](-0.2, 0.2));
      for(int i = 0, j = random[Lith_GunSmoke](m_Part + 1, m_Part + 3); i < j; i++) {
         lith_spawnParticles("Yellow");
      }
      if(lith_sv_lessparticles) {
         return;
      }
      for(int i = 0, j = random[Lith_GunSmoke](m_Part + 1, m_Part + 2); i < j; i++) {
         lith_spawnParticles("White");
      }
      for(int i = 0, n = random[Lith_GunSmoke](m_Part/2, m_Part); i < n; i++) {
         lith_spawnSmoke(fRandom[Lith_GunSmoke](-10, 10), fRandom[Lith_GunSmoke](-10, 10));
      }
      destroy();
   }
}

class Lith_GunSmokeAndSparksSmall : Lith_GunSmokeAndSparks {
   default {
      Lith_GunSmokeAndSparks.Particles 5;
   }
}

class Lith_GunSmokeAndSparksBig : Lith_GunSmokeAndSparks {
   default {
      Lith_GunSmokeAndSparks.Particles 15;
      Lith_GunSmokeAndSparks.SmokeClass "Lith_GunSmokeBig";
      Lith_GunSmokeAndSparks.Spread 3;
   }
}

/* EOF */
