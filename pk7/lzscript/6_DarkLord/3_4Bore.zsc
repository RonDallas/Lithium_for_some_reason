// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_4BoreDamageBuff : Inventory {
   default {
      Inventory.MaxAmount 8;
   }
   override void postBeginPlay() {
      destroy();
   }
}

class Lith_4BorePuff : Lith_BulletPuff {
   default {
      +PuffOnActors
      Scale 2;
   }
   static const color[] CRS = {
      "#43f476",
      "#68f443",
      "#c1f443",
      "#f4cf43",
      "#f47643",
      "#f45943",
      "#f4434b",
      "#f44368"
   };
   override void postBeginPlay() {
      super.postBeginPlay();
      if(tracer && target && Lith_UTIL.pData(_pdt_upgrade, UPGR_4Bore_A)) {
         int bonus = tracer.countInv('Lith_4BoreDamageBuff');
         tracer.giveInventory('Lith_4BoreDamageBuff', 1);
         if(bonus) {
            tracer.damageMobj(target, self, 15 * bonus, 'None', DMG_USEANGLE|DMG_PLAYERATTACK);
            Lith_URANUS.instance().particle4BoreDamageBuff(pos, CRS[bonus]);
         }
      }
   }
}

class Lith_4Bore : Lith_DWeapon;

default {
   Lith_Weapon.SpawnType "Lith_SuperShotgunPickup";
   Tag "$LITH_INFO_SHORT_4Bore";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 0.9;
   Weapon.UpSound "weapons/4bore/draw";
   Lith_Weapon.AmmoType "Lith_ShellAmmo";
   Inventory.AltHudIcon "4BORZ0";
}

override void markPrecacheSounds() {
   super.markPrecacheSounds();
   markSound("weapons/empty");
   markSound("weapons/4bore/fire");
   markSound("weapons/4bore/open");
   markSound("weapons/4bore/load");
   markSound("weapons/4bore/close");
   markSound("weapons/4bore/empty");
}

action(Weapon) void Lith_A_Fire4Bore() {
   SoundAlert(Invoker.Owner);
   lith_takeAmmo(4);
   A_SetBlend("White", 0.2, 6);
   lith_gunFlash();
   soundAlert(invoker.owner);
   Lith_A_GunSmoke(1, 6);
   A_FireProjectile("Lith_LightProjectileRedLong", 0);
   Lith_FireBullets(4, 2, 8, random[lith_dlweapon](125, 150), FBF_NORANDOM, puffType: 'Lith_4BorePuff', tracerX: 2, tracer: _tr_big);
   Lith_A_RecoilUp(10);
   a_recoil(4);
}

states(Weapon) {
Ready:
   4BOR A 1 Lith_A_Ready;
   loop;

Deselect:
   4BOR A 1 Lith_A_Lower(_raise_normal);
   loop;
Select:
   4BOR A 1 Lith_A_Raise(_raise_normal);
   loop;

Fire:
   #### # 0 lith_jumpNoAmmo(4);
   4BOR A 7 A_StartSound("weapons/4bore/fire", lch_weapon);
   4BOR G 1 offset(140, 121) Lith_A_Fire4Bore;
   4BOR H 1 bright offset(89, 71);
   4BOR B 2 bright offset(97, 77);
   4BOR G 2 offset(176, 155);
   4BOR A 1 offset(45, 80);
   #### # 0 lith_a_setInterpolation(true);
   4BOR A 1 offset(30, 60);
   4BOR A 1 offset(25, 50);
   4BOR A 1 offset(20, 48);
   4BOR A 1 offset(15, 45);
   4BOR A 1 offset(11, 44);
   4BOR A 1 offset(8, 41);
   4BOR A 1 offset(5, 39);
   4BOR A 1 offset(3, 36);
   4BOR A 1 offset(2, 34);
   4BOR A 1 offset(-3, 32);
   4BOR A 1 offset(-5, 32);
   4BOR A 1 offset(-10, 33);
   4BOR A 1 offset(-22, 36);
   #### # 0 lith_a_setInterpolation(false);
   4BOR C 1 offset(19, 130);
   #### # 0 lith_a_setInterpolation(true);
   4BOR C 1 offset(4, 135);
   4BOR C 1 offset(-22, 143);
   #### # 0 lith_a_setInterpolation(false);
   4BOR D 1 offset(-28, 166) {
      A_StartSound("weapons/4bore/open", lch_reload);
      Lith_A_Casing("Lith_Cas4Bore", 15, -1, 40, FRandom(3,4), FRandom(7,8), FRandom(2,9));
      Lith_A_Casing("Lith_Cas4Bore", 15, -4, 40, FRandom(3,4), FRandom(7,8), FRandom(2,9));
   }
   #### # 0 lith_a_setInterpolation(true);
   4BOR D 1 offset(-41, 172);
   4BOR D 1 offset(-56, 186);
   4BOR D 1 offset(-71, 201);
   #### # 0 lith_a_setInterpolation(false);
   TNT1 A 12;
   TNT1 A 6 A_StartSound("weapons/4bore/load", lch_reload2);
   4BOR F 1 offset(-43, 182) Lith_A_ReloadBobUp(1);
   #### # 0 lith_a_setInterpolation(true);
   4BOR F 1 offset(-30, 162);
   4BOR F 1 offset(-25, 155);
   4BOR F 1 offset(-8, 137);
   4BOR E 2 offset(55, 96) A_StartSound("weapons/4bore/close", lch_reload3);
   4BOR E 1 offset(59, 95);
   4BOR E 3 offset(60, 97);
   4BOR E 1 offset(60, 95);
   4BOR E 2 offset(61, 98);
   4BOR E 2 offset(63, 96);
   #### # 0 lith_a_setInterpolation(false);
   4BOR C 1 offset(-2, 112);
   4BOR C 1 offset(11, 115);
   4BOR A 1 offset(-27, 32);
   4BOR A 1 offset(-4, 35);
   4BOR A 1 offset(5, 33);
   goto ReadySelector;
FireEmpty:
   4BOR A 2 offset(3, 38);
   4BOR A 1 offset(7, 40);
   4BOR A 1 offset(5, 37) A_StartSound("weapons/4bore/empty", lch_dryweapon);
   4BOR A 1 offset(4, 34);
   4BOR A 1 offset(2, 33);
   4BOR A 5 offset(1, 32);
   goto ReadySelector;
}

/* EOF */
