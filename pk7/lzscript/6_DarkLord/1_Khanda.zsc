// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_StatSkillPower : Lith_StatFx {
   override void initCb() {basic("SKP", 35 * 5);}
}

extend class Lith_HERMES {
   static bool KhandaPower(Actor mo) {
      Lith_StatFx.addToPool('Lith_StatSkillPower');
      return false;
   }
}

class Lith_KhandaPuff : Lith_BulletPuff {
   default {
      Mass 5;
      +PuffOnActors
      +ThruGhost
   }
   override void postBeginPlay() {
      super.postBeginPlay();
      if(tracer) {
         if(!tracer.bNoBlood && !tracer.bDormant) {
            a_startSound("body/flesh", lch_auto);
         }
         if(target) {
            target.a_startSound("weapons/sword/hit", lch_weapon3);
         }
      } else if(target) {
         target.a_startSound("weapons/sword/swing1", lch_weapon3);
      }
   }
   override void markPrecacheSounds() {
      super.markPrecacheSounds();
      markSound("weapons/sword/swing1");
      markSound("weapons/sword/hit");
      markSound("body/flesh");
   }
}

class Lith_Khanda : Lith_DWeapon;

default {
   Lith_Weapon.SpawnType "Lith_FistPickup";
   Lith_Weapon.DefaultSpeed 1.9;
   Lith_Weapon.SprintSpeed 2.4;
   Tag "$LITH_INFO_SHORT_Khanda";
   Weapon.SlotNumber 1;
   Weapon.SlotPriority 0.9;
   Weapon.UpSound "weapons/khanda/draw";
   Inventory.AltHudIcon "KAMLZ0";
   +Weapon.NoAutoFire
}
bool m_power;
override void markPrecacheSounds() {
   super.markPrecacheSounds();
   markSound("weapons/khanda/swing");
   markSound("weapons/khanda/pwup");
}
override void tick() {
   super.tick();
   m_power = Lith_StatFx.getInPool('Lith_StatSkillPower');
}
states(Weapon) {
PowerOverlay:
   TNT1 A 1 {
      let wep = player.findPSprite(_psp_weapon);
      let psp = player.findPSprite(_psp_d_khanda_power);
      if(wep && psp) {
         Lith_UTIL.copyPSpriteVisualData(psp, wep);
         /* FIXME: these two should just be normal set ops,
                   but you can only use these functions to set these
         */
         a_overlayFlags(_psp_d_khanda_power, pspf_renderstyle|pspf_alpha, true);
         a_overlayRenderStyle(_psp_d_khanda_power, STYLE_Add);
         psp.alpha = (sin(gameTic / 17.5 * 360) + 1.0) / 2.5;
         psp.x = psp.y = psp.oldx = psp.oldy = 0;
         psp.bAddWeapon = true;
         psp.translation = Translate.getId('Lith_KhandaPower');
      }
      if(!invoker.m_power) {
         if(psp && psp.alpha > 0) {
            psp.alpha -= 0.1;
         }
         if(!psp || psp.alpha <= 0) {
            return resolveState("Null");
         }
      }
      return state(null);
   }
   wait;
Ready:
   KHAN A 1 {
      if(invoker.m_power) {
         a_overlay(_psp_d_khanda_power, "PowerOverlay", true);
      }
      return lith_a_ready(WRF_ALLOWRELOAD);
   }
   wait;
Deselect:
   KHAN A 1 lith_a_lower(_raise_fast);
   wait;
Select:
   KHAN A 1 lith_a_raise(_raise_fast);
   wait;
Fire:
   #### # 0 lith_a_setInterpolation(true);
   KHAN A 1 offset(-35, 32);
   KHAN B 1 offset(-165, 137);
   KHAN B 1 offset(-190, 134);
   KHAN C 2 offset(-198, 124);
   KHAN C 3 offset(-205, 126);
Hold:
   KHAN D 1 offset(-103, 143) a_startSound("weapons/khanda/swing", lch_weapon);
   #### # 0 lith_a_setInterpolation(true);
   KHAN E 1 offset(163, 120) lith_a_fireMelee((invoker.m_power ? 50 : 1) * (countInv("PowerStrength") ? 40 : 20), 128, _mlf_pull_in|_mlf_no_turn, "Lith_KhandaPuff");
   #### # 0 lith_a_setInterpolation(false);
   KHAN E 1 offset(259, 175);
   TNT1 A 3 a_weaponOffset(0, 32);
   KHAN F 1 offset(148, 170);
   #### # 0 lith_a_setInterpolation(true);
   KHAN G 1 offset(80, 98) a_startSound("weapons/khanda/swing", lch_weapon2);
   KHAN H 1 offset(-25, 40) lith_a_fireMelee((invoker.m_power ? 50 : 1) * (countInv("PowerStrength") ? 60 : 30), 128, _mlf_pull_in|_mlf_no_turn|_mlf_damage_stepup, "Lith_KhandaPuff");
   KHAN H 1 offset(-130, 12);
   #### # 0 lith_a_setInterpolation(false);
   KHAN I 1 offset(-124, -21);
   TNT1 A 3 a_weaponOffset(0, 32);
   TNT1 A 0 a_reFire;
   KHAN A 3 offset(-126, 71);
   #### # 0 lith_a_setInterpolation(true);
   KHAN A 1 offset(-88, 43);
   KHAN A 1 offset(-50, 32);
   KHAN A 1 offset(-15, 32);
   KHAN A 1 offset(-4, 32);
   KHAN A 1 offset(-1, 32);
   #### # 0 lith_a_setInterpolation(false);
   goto ReadySelector;
}

/* EOF */
